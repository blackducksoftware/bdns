initscript {
	repositories { jcenter() }
	dependencies { classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.4.18' }
	dependencies { classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3' }
	dependencies { classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.1' }
}

// Travis inspiration from:
//  https://github.com/square/okhttp/blob/master/.buildscript/deploy_snapshot.sh
//  https://coderwall.com/p/9b_lfq
//  http://benlimmer.com/2013/12/26/automatically-publish-javadoc-to-gh-pages-with-travis-ci/

// Jacoco/Coveralls inspiration from:
//  https://gist.github.com/aalmiray/3e0e70cba66effdb449a
//  https://github.com/ben-manes/caffeine/blob/master/build.gradle

// Applying plugins must be done by class name instead of plugin identifier:
//  http://stackoverflow.com/questions/14595606

// It does not seem possible to apply another init script from an init script...

if (System.getenv('TRAVIS_REPO_SLUG') == 'blackducksoftware/bdns'
	&& (System.getenv('TRAVIS_BRANCH') == 'master' || System.getenv('TRAVIS_TAG') != null)
	&& !Boolean.getBoolean('TRAVIS_PULL_REQUEST')) {

	// Run Jacoco coverage and publish to Coveralls
	projectsEvaluated {
		rootProject {
			apply plugin: 'jacoco'
			apply plugin: org.kt3k.gradle.plugin.CoverallsPlugin
			jacocoTestReport {
				sourceDirectories = files(sourceSets.main.allSource.srcDirs)
				classDirectories = files(sourceSets.main.output)
				reports {
					html.enabled = false
					xml.enabled = true
				}
			}
			publish.dependsOn tasks.coveralls
			
			// Run the root report from `script:` instead of `after_success:`
			check.dependsOn jacocoTestReport
		}
	}

	// Deploy artifacts
	rootProject {
		if (version.endsWith('-SNAPSHOT')) {
			// Snapshot version goes to oss.jfrog.org
			apply plugin: org.jfrog.gradle.plugin.artifactory.ArtifactoryPlugin
			
			artifactory {
				publish {
					contextUrl = 'https://oss.jfrog.org/artifactory'
					repository {
						repoKey = 'oss-snapshot-local'
						username = System.getenv('BINTRAY_USER')
						password = System.getenv('BINTRAY_KEY')
					}
					defaults {
						publications('myPublication')
					}
				}
			}
	
			afterEvaluate {
				publish.dependsOn artifactoryPublish
				
				jar.manifest.attributes['Implementation-Version'] += '+build.' + System.getenv('TRAVIS_BUILD_NUMBER')
			}
		} else {
			// Release version goes to bintray.com
			apply plugin: com.jfrog.bintray.gradle.BintrayPlugin
	
			bintray {
				user = System.getenv('BINTRAY_USER')
				key = System.getenv('BINTRAY_KEY')
				publications = [ 'myPublication' ]
				publish = true
				pkg {
					repo = 'oss'
					name = 'bdns'
					userOrg = 'bds'
				}
			}
	
			afterEvaluate {
				publish.dependsOn bintrayUpload
			}
		}
	}
}
